#compdef -k menu-select ^x^g

function _complete-glob() {

    local gresult format REPLY

    # get glob results
    gresult=( ${~words[$CURRENT]}(N) )
    (( $#gresult > 0 )) || { _message "No matches for $PREFIX" && return 0 }

    # we can't use _description, for some reason it screws up menu-select! sheesh!
    # gotta find the formats ourselves... anyone got a better solution, let me know.
    zstyle -s ":completion:${curcontext}:$1" format format \
        || zstyle -s ":completion:${curcontext}:descriptions" format format \
        || format="%d"

    # add the old glob, for good measure
    zformat -f REPLY "$format" "d:Original glob"
    compadd -U -Q -V glob -X $REPLY -- $PREFIX

    # clear prefix
    PREFIX=

    # print
    # print - $expl
    zformat -f REPLY "$format" "d:Glob matches"
    compadd -V matches -X $REPLY -f -a gresult

}

_complete-glob "$@"
